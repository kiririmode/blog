---
Title: B::Deparse で One-Liner を理解する
Date: 2020-11-07T12:00:48+09:00
URL: https://kiririmode.hatenablog.jp/entry/20201107/1604718048
EditURL: https://blog.hatena.ne.jp/kiririmode/kiririmode.hatenablog.jp/atom/entry/26006613649884754
Category:
- perl
---

久しぶりに会社で Perl の話をしました。何の話かというと [B::Deparse](https://perldoc.perl.org/B::Deparse)です。

最近 Perl を使うのは One-Liner くらいなのですが、一般に One-Liner は可読性が犠牲になります。
一体この One-Liner 何をしているの？というのを説明するときには B::Deparse を使うことで説明が容易になります。

例えば、以下のような Perl の One-Liner があるとします。

```shell
$ perl -pi'.orig' -e 's/bar/baz/' fileA
```

この One-Liner の意味するところは以下になります。

- ファイル `fileA` の中の `bar` を `baz` に置換せよ
- ただし、元ファイルは `fileA.orig` としてバックアップしておけ

これをわかりやすくするのが `B::Deparse` です。まずは具体的に使ってみましょう。

```shell
$ perl -MO=Deparse -pi'.orig' -e 's/bar/baz/' fileA
BEGIN { $^I = ".orig"; }
LINE: while (defined($_ = <ARGV>)) {
    s/bar/baz/;
}
continue {
    die "-p destination: $!\n" unless print $_;
}
-e syntax OK
```

ずっとわかりやすくなりました。`B::Deparse` は、Perl のコンパイラが生成した中間コードを解析した上でソースコードを復元してくれます。
これを利用すれば、One-Liner が具体的にどういう処理をしているのかがコードとして理解できるようになります。
(`$^I` や `$!` がわかんないという場合は、`perldoc perlvar` をご参照ください)


ほかにも、例えば `Try::Tiny` を使えば仮想的に Perl で Try-Catch-Finally を実現することができます。

```perl
# handle errors with a catch handler
try {
  die "foo";
} catch {
  warn "caught error: $_"; # not $@
};
```

一方で、Perl では `try` や `catch` を文法的にサポートしているわけではありません。
これはどのようにして実現されているのか。こちらも `B::Deparse` を利用して確認してみましょう。

```shell
$ perl -MO=Deparse -MTry::Tiny -e 'try { die "foo"; } catch { warn "caught error: $_"; }'
use Try::Tiny;
try sub {
    die 'foo';
}
, catch(sub {
    warn "caught error: $_";
}
);
-e syntax OK
```

`try` のあとに `sub` があり、無名関数として宣言されていることがわかります。さらに、その無名関数のあとには `,` があり、`catch` のあとにまた無名関数が現れています。
これは、以下のような構造になっていることを意味しています。

```perl
try (sub {...}, catch(sub {...}))
```

要するに: 

- try 関数の引数として無名関数一つと、catch 関数の呼び出し結果が渡されている
- catch 関数には、無名関数が渡されている

もちろん、実体を理解するには Perl における [Prototype](https://perldoc.perl.org/perlsub#Prototypes)の知識が必要ではありますが。

One-Liner を含め、いったいこのコードは何をしているんだろう、という疑問があるときは `B::Deparse` を利用するとヒントが得られるかなと。
