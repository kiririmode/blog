---
Title: ' 問2-56 (2.3.2 Example: Symbolic Differentiation)'
Category:
- SICP
Date: 2007-12-22T00:00:01+09:00
URL: https://kiririmode.hatenablog.jp/entry/20071222/p2
EditURL: https://blog.hatena.ne.jp/kiririmode/kiririmode.hatenablog.jp/atom/entry/8454420450078215860
---


数値的微分じゃなくて、記号的微分（Symbolic Differentiation）やろうぜって章に入りました。
最初はどうやってやるんだっていうか無理じゃねみたく思っていたのですが、簡単なヤツなら結構簡単にできるみたいです。簡単って言うのも、コロンブスの卵だと思うんですけど。

既に本文で
[tex: \frac{dc}{dx}=0 ]、[tex:\frac{dx}{dx}=1]、[tex:\frac{d(u+v)}{dx}=\frac{du}{dx}+\frac{dv}{dx}]、[tex: \frac{d(uv)}{dx}=u\left(\frac{dv}{dx}\right)+ v\left(\frac{du}{dx}\right)]というルールは用意されていて、ここに[tex: \frac{d(u^n)}{dx} = nu^{n-1}\left(\frac{du}{dx}\right)]の規則を埋めろって課題です。

本文で何をすれば良いのかも書いてあるので、まずは構築子と選択子、述語を作って、
>|scheme|
(define (exponentiation? x) ; 述語
  (and (pair? x) (eq? (car x) '**))) 
(define (base p) (cadr p)) ; 選択子
(define (exponent p) (caddr p)) ; 選択子
(define (make-exponentiation base exponent) ; 構築子
  (cond ((=number? exponent 0) 1)
	((=number? exponent 1) base)
	(else (list '** base exponent))))
||<
微分用関数はこれ。
>|scheme|
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
	((exponentiation? exp)
	 (make-product (exponent exp)
		       (make-product (deriv (base exp) var)
				     (make-exponentiation (base exp)
							  (- (exponent exp) 1)))))
	((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
           (make-product (multiplier exp)
                         (deriv (multiplicand exp) var))
           (make-product (deriv (multiplier exp) var)
                         (multiplicand exp))))
        (else
         (error "unknown expression type -- DERIV" exp))))
||<

[tex: (5x + y)^5]を微分してみると
>|scheme|
(deriv '(** (+ (* 5 x ) y) 5) 'x)  ; (* 5 (* 5 (** (+ (* 5 x) y) 4)))
||<
[tex:5\cdot 5( 5x + y )^4]という答えがでてきます。ちとシンプルじゃないな。
