---
Title: 問題1-40
Category:
- SICP
Date: 2007-10-22T00:00:01+09:00
URL: https://kiririmode.hatenablog.jp/entry/20071022/p2
EditURL: https://blog.hatena.ne.jp/kiririmode/kiririmode.hatenablog.jp/atom/entry/8454420450078216465
---


ニュートン法で[tex:x^3+ax^2+bx+c]の零点を近似する。
>|scheme|
(define dx 0.00001)

(define (deriv g)
  (lambda (x)
    (/ (- (g (+ x dx)) (g x))
       dx)))

(define (newton-transform g)
  (lambda (x)
    (- x (/ (g x) ((deriv g) x)))))

(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
	  next
	  (try next))))
  (try first-guess))

(define (newtons-method g guess)
  (fixed-point (newton-transform g) guess))

(define (cube x) (* x x x))
(define (square x) (* x x))

(define (cubic a b c)
  (lambda (x)
    (+ (cube x)
       (* a (square x))
       (* b x)
       c)))
||<
答えが簡単にわかるので、[tex:x^3+8]の零点を計算します！
>|scheme|
gosh> (newtons-method (cubic 0 0 8) 1)
-1.999999999999997
||<
